start: pipe_rule
%import common.WORD   // imports from terminal library
%ignore " "           // Disregard spaces in text
pipe_rule: or_rule ["|" aggregation_expression]
or_rule: and_rule (("or"|"OR") and_rule)*
and_rule: not_rule (("and"|"AND") not_rule)*
not_rule: [not] atom
not: "NOT" | "not"
atom: x_of | search_id | "(" pipe_rule ")"
search_id: SEARCH_ID
x: ALL | NUMBER
x_of: x OF search_pattern
search_pattern: /[a-zA-Z*_][a-zA-Z0-9*_]*/
aggregation_expression: aggregation_function "(" [aggregation_field] ")" [ "by" group_field ] comparison_op value
                      | near_aggregation
aggregation_function: COUNT | MIN | MAX | AVG | SUM
near_aggregation: "near" or_rule
aggregation_field: SEARCH_ID
group_field: SEARCH_ID
comparison_op: GT | LT | EQ
GT: ">"
LT: "<"
EQ: "="
value: NUMBER
NUMBER: /[1-9][0-9]*/
NOT: "NOT"
SEARCH_ID: /[a-zA-Z_][a-zA-Z0-9_]*/
ALL: "all"
OF: "of"
COUNT: "count"
MIN: "min"
MAX: "max"
AVG: "avg"
SUM: "sum"
